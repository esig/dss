<?xml version="1.0" encoding="UTF-8" ?>
<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:dss="http://dss.esig.europa.eu/validation/simple-report">
                
	<xsl:output method="html" encoding="utf-8" indent="yes" omit-xml-declaration="yes" />

    <xsl:template match="/dss:SimpleReport">
		<xsl:comment>Generated by DSS v.${project.version}</xsl:comment>
	    <xsl:apply-templates/>
	    <xsl:call-template name="documentInformation"/>
    </xsl:template>

    <xsl:template match="dss:DocumentName"/>
    <xsl:template match="dss:SignatureFormat"/>
    <xsl:template match="dss:SignaturesCount"/>
    <xsl:template match="dss:ValidSignaturesCount"/>
    <xsl:template match="dss:ValidationTime"/>
    <xsl:template match="dss:ContainerType"/>

    <xsl:template match="dss:ValidationPolicy">
		<div>
    		<xsl:attribute name="class">panel panel-primary</xsl:attribute>
    		<div>
    			<xsl:attribute name="class">panel-heading</xsl:attribute>
	    		<xsl:attribute name="data-target">#collapsePolicy</xsl:attribute>
		       	<xsl:attribute name="data-toggle">collapse</xsl:attribute>
    			Validation Policy : <xsl:value-of select="dss:PolicyName"/>
	        </div>
    		<div>
    			<xsl:attribute name="class">panel-body collapse in show</xsl:attribute>
	        	<xsl:attribute name="id">collapsePolicy</xsl:attribute>
	        	<p>
	        		<xsl:value-of select="dss:PolicyDescription"/>
	        	</p>
    		</div>
    	</div>
    </xsl:template>

    <xsl:template match="dss:Signature|dss:Timestamp">
        <xsl:variable name="indicationText" select="dss:Indication/text()"/>
        <xsl:variable name="idToken" select="@Id" />
        <xsl:variable name="nodeName" select="name()" />
        <xsl:variable name="indicationCssClass">
        	<xsl:choose>
				<xsl:when test="$indicationText='TOTAL_PASSED'">success</xsl:when>
				<xsl:when test="$indicationText='PASSED'">success</xsl:when>
				<xsl:when test="$indicationText='INDETERMINATE'">warning</xsl:when>
				<xsl:when test="$indicationText='FAILED'">danger</xsl:when>
				<xsl:when test="$indicationText='TOTAL_FAILED'">danger</xsl:when>
			</xsl:choose>
        </xsl:variable>
        
        <div>
    		<xsl:attribute name="class">panel panel-<xsl:value-of select="$indicationCssClass" /></xsl:attribute>
    		<div>
    			<xsl:attribute name="class">panel-heading</xsl:attribute>
	    		<xsl:attribute name="data-target">#collapseSig<xsl:value-of select="$idToken" /></xsl:attribute>
		       	<xsl:attribute name="data-toggle">collapse</xsl:attribute>
		       	
		       	<xsl:if test="@CounterSignature = 'true'">
					<span>
			        	<xsl:attribute name="class">label label-info pull-right</xsl:attribute>
						Counter-signature
		        	</span>
				</xsl:if>
		       	
				<xsl:if test="$nodeName = 'Signature'">
					Signature
				</xsl:if>
				<xsl:if test="$nodeName = 'Timestamp'">
					Timestamp
				</xsl:if>
		       	
    			<xsl:value-of select="$idToken" />
	        </div>
    		<div>
    			<xsl:attribute name="class">panel-body collapse in show</xsl:attribute>
				<xsl:attribute name="id">collapseSig<xsl:value-of select="$idToken" /></xsl:attribute>
				
				<xsl:if test="dss:Filename">
					<dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			    		
						<xsl:if test="$nodeName = 'Signature'">
			            	<dt>Signature filename:</dt>
						</xsl:if>
						<xsl:if test="$nodeName = 'Timestamp'">
			            	<dt>Timestamp filename:</dt>
						</xsl:if>
			            <dd>
							<xsl:value-of select="dss:Filename" />
			        	</dd>
			        </dl>
				</xsl:if>
				
				<xsl:if test="dss:SignatureLevel | dss:TimestampLevel">
					<dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>Qualification:</dt>
			            <dd>
			            
							<xsl:if test="dss:SignatureLevel">
								<xsl:value-of select="dss:SignatureLevel" />
							</xsl:if>
							<xsl:if test="dss:TimestampLevel">
								<xsl:value-of select="dss:TimestampLevel" />
							</xsl:if>
							<span>
				    			<xsl:attribute name="class">glyphicon glyphicon-info-sign text-info</xsl:attribute>
								<xsl:attribute name="style">margin-left : 10px</xsl:attribute>
								<xsl:attribute name="data-toggle">tooltip</xsl:attribute>
								<xsl:attribute name="data-placement">right</xsl:attribute>
								
								<xsl:if test="dss:SignatureLevel">
									<xsl:attribute name="title"><xsl:value-of select="dss:SignatureLevel/@description" /></xsl:attribute>
								</xsl:if>
								<xsl:if test="dss:TimestampLevel">
									<xsl:attribute name="title"><xsl:value-of select="dss:TimestampLevel/@description" /></xsl:attribute>
								</xsl:if>
				    		</span>					
			        	</dd>
			        </dl>
				</xsl:if>

				<xsl:apply-templates select="dss:QualificationDetails" />

				<xsl:if test="@SignatureFormat">
			        <dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>Signature format:</dt>
			            <dd><xsl:value-of select="@SignatureFormat"/></dd>
			        </dl>
		        </xsl:if>
			
				<dl>
					<xsl:attribute name="class">dl-horizontal</xsl:attribute>
					<dt>Indication:</dt>
					<dd>
						<xsl:attribute name="class">text-<xsl:value-of select="$indicationCssClass" /></xsl:attribute>
						<xsl:choose>
							<xsl:when test="$indicationText='TOTAL_PASSED'">
								<span>
									<xsl:attribute name="class">glyphicon glyphicon-ok-sign</xsl:attribute>
								</span>
							</xsl:when>
							<xsl:when test="$indicationText='PASSED'">
								<span>
									<xsl:attribute name="class">glyphicon glyphicon-ok-sign</xsl:attribute>
								</span>
							</xsl:when>
							<xsl:when test="$indicationText='INDETERMINATE'">
								<span>
									<xsl:attribute name="class">glyphicon glyphicon-question-sign</xsl:attribute>
								</span>
							</xsl:when>
							<xsl:when test="$indicationText='FAILED'">
								<span>
									<xsl:attribute name="class">glyphicon glyphicon-remove-sign</xsl:attribute>
								</span>
							</xsl:when>
							<xsl:when test="$indicationText='TOTAL_FAILED'">
								<span>
									<xsl:attribute name="class">glyphicon glyphicon-remove-sign</xsl:attribute>
								</span>
							</xsl:when>
						</xsl:choose>
			
						<xsl:text> </xsl:text>
						<xsl:value-of select="dss:Indication" />
					</dd>
				</dl>   
		        
		        <xsl:apply-templates select="dss:SubIndication">
		            <xsl:with-param name="indicationClass" select="$indicationCssClass"/>
		        </xsl:apply-templates>

				<xsl:apply-templates select="dss:AdESValidationDetails" />

		        <dl>
	        		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
		            <dt>Certificate Chain:</dt>
		            <xsl:choose>
			            <xsl:when test="dss:CertificateChain">
				            <xsl:for-each select="dss:CertificateChain/dss:Certificate">
				            	<xsl:variable name="index" select="position()"/>
				        		<dd>
			        				<span><xsl:attribute name="class"> glyphicon glyphicon-link</xsl:attribute></span>
				        			<xsl:choose>
				        				<xsl:when test="$index = 1">
				        					<b> <xsl:value-of select="dss:qualifiedName" /></b>
				        				</xsl:when>
				        				<xsl:otherwise>
											<xsl:value-of select="dss:qualifiedName" />				        				
				        				</xsl:otherwise>
				        			</xsl:choose>
			        			</dd>
				        	</xsl:for-each>
			        	</xsl:when>
			        	<xsl:otherwise>
			        		<dd>/</dd>
			        	</xsl:otherwise>
		        	</xsl:choose>
	        	</dl>
		        
				<xsl:if test="dss:SigningTime">
			        <dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>On claimed time:</dt>
			            <dd><xsl:value-of select="dss:SigningTime"/></dd>
			        </dl>
		        </xsl:if>
		        
				<xsl:if test="dss:ProductionTime">
			        <dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>Production time:</dt>
			            <dd><xsl:value-of select="dss:ProductionTime"/></dd>
			        </dl>
		        </xsl:if>
		        
				<xsl:if test="dss:BestSignatureTime">
			        <dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>Best signature time:</dt>
			            <dd>
			            	<xsl:value-of select="dss:BestSignatureTime"/>
			            	<span>
				    			<xsl:attribute name="class">glyphicon glyphicon-info-sign text-info</xsl:attribute>
								<xsl:attribute name="style">margin-left : 10px</xsl:attribute>
								<xsl:attribute name="data-toggle">tooltip</xsl:attribute>
								<xsl:attribute name="data-placement">right</xsl:attribute>
								<xsl:attribute name="title">
									Lowest time at which there exists a proof of existence for the signature 
								</xsl:attribute>
				    		</span>		
			            </dd>
			        </dl>
		        </xsl:if>
		        
				<xsl:if test="$nodeName = 'Signature'">
			        <dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>Signature position:</dt>
			            <dd><xsl:value-of select="count(preceding-sibling::dss:Signature) + 1"/> out of <xsl:value-of select="count(ancestor::*/dss:Signature)"/></dd>
			        </dl>
				</xsl:if>
		        
				<xsl:if test="dss:SignatureScope">
			        <xsl:for-each select="dss:SignatureScope">
				        <dl>
				    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
				            <dt>Signature scope:</dt>
				            <dd><xsl:value-of select="@name"/> (<xsl:value-of select="@scope"/>)</dd>
				            <dd><xsl:value-of select="."/></dd>
				        </dl>
			        </xsl:for-each>
		        </xsl:if>

				<xsl:if test="dss:Timestamps">
					<div>
						<xsl:attribute name="class">panel panel-default mt-3</xsl:attribute>
						<div>
							<xsl:attribute name="class">panel-heading collapsed</xsl:attribute>
							<xsl:attribute name="data-target">#collapseSigDetails<xsl:value-of select="$idToken" /></xsl:attribute>
							<xsl:attribute name="data-toggle">collapse</xsl:attribute>
							<xsl:attribute name="aria-expanded">false</xsl:attribute>

							Timestamps <span class="badge badge-light"><xsl:value-of select="count(dss:Timestamps/dss:Timestamp)" /></span>
						</div>
						<div>
							<xsl:attribute name="class">panel-body collapse pb-1</xsl:attribute>
							<xsl:attribute name="id">collapseSigDetails<xsl:value-of select="$idToken" /></xsl:attribute>
							<xsl:apply-templates select="dss:Timestamps" />
						</div>
					</div>
				</xsl:if>
		        
    		</div>
    	</div>
    </xsl:template>

	<xsl:template match="dss:AdESValidationDetails|dss:QualificationDetails">
		<xsl:variable name="header">
			<xsl:choose>
				<xsl:when test="name() = 'AdESValidationDetails'">AdES Validation Details</xsl:when>
				<xsl:when test="name() = 'QualificationDetails'">Qualification Details</xsl:when>
			</xsl:choose>
		</xsl:variable>
		<dl>
			<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			<dt><xsl:value-of select="$header" /> :</dt>
			<dd></dd>
			<xsl:apply-templates select="dss:Error" />
			<xsl:apply-templates select="dss:Warning" />
			<xsl:apply-templates select="dss:Info" />
		</dl>
	</xsl:template>

	<xsl:template match="dss:Error|dss:Warning|dss:Info">
		<xsl:variable name="style">
			<xsl:choose>
				<xsl:when test="name() = 'Error'">danger</xsl:when>
				<xsl:when test="name() = 'Warning'">warning</xsl:when>
				<xsl:otherwise>auto</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>
		<dd>
			<xsl:attribute name="class">text-<xsl:value-of select="$style" /></xsl:attribute>
			<xsl:value-of select="." />
		</dd>
	</xsl:template>

	<xsl:template match="dss:Timestamps">
		<div>
			<xsl:apply-templates select="dss:Timestamp">
				<xsl:with-param name="cardStyle" select="'light'"/>
			</xsl:apply-templates>
		</div>
	</xsl:template>

	<xsl:template match="dss:SubIndication">
		<xsl:param name="indicationClass" />
		<dl>
    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			<dt>Sub indication:</dt>
			<dd>
				<xsl:attribute name="class">text-<xsl:value-of select="$indicationClass" /></xsl:attribute>
				<xsl:value-of select="." />
			</dd>
		</dl>
	</xsl:template>

    <xsl:template name="documentInformation">
		<div>
    		<xsl:attribute name="class">panel panel-primary</xsl:attribute>
    		<div>
    			<xsl:attribute name="class">panel-heading</xsl:attribute>
	    		<xsl:attribute name="data-target">#collapseInfo</xsl:attribute>
		       	<xsl:attribute name="data-toggle">collapse</xsl:attribute>
    			Document Information
	        </div>
    		<div>
    			<xsl:attribute name="class">panel-body collapse in show</xsl:attribute>
	        	<xsl:attribute name="id">collapseInfo</xsl:attribute>
	        	
				<xsl:if test="dss:ContainerType">
			        <dl>
			    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
			            <dt>Container type:</dt>
			            <dd><xsl:value-of select="dss:ContainerType"/></dd>
			        </dl>
		        </xsl:if>
	        	<dl>
		    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
		            <dt>Signatures status:</dt>
		            <dd>
		                <xsl:choose>
		                    <xsl:when test="dss:ValidSignaturesCount = dss:SignaturesCount">
		                        <xsl:attribute name="class">text-success</xsl:attribute>
		                    </xsl:when>
		                    <xsl:otherwise>
		                        <xsl:attribute name="class">text-warning</xsl:attribute>
		                    </xsl:otherwise>
		                </xsl:choose>
		                <xsl:value-of select="dss:ValidSignaturesCount"/> valid signatures, out of <xsl:value-of select="dss:SignaturesCount"/>
		            </dd>
		        </dl>
		        <dl>
		    		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
		            <dt>Document name:</dt>
		            <dd><xsl:value-of select="dss:DocumentName"/></dd>
		        </dl>
		        
    		</div>
    	</div>
    </xsl:template>
</xsl:stylesheet>
